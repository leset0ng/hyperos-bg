export const OS3_FRAGMENT_SHADER_SOURCE = `
precision highp float;

uniform vec2 uResolution;
uniform float uAnimTime;
uniform vec4 uBound;
uniform float uTranslateY;
uniform vec3 uPoints[4];
uniform vec4 uColors[4];
uniform float uAlphaMulti;
uniform float uNoiseScale;
uniform float uPointOffset;
uniform float uPointRadiusMulti;
uniform float uSaturateOffset;
uniform float uLightOffset;
uniform float uAlphaOffset;
uniform float uShadowColorMulti;
uniform float uShadowColorOffset;
uniform float uShadowNoiseScale;
uniform float uShadowOffset;
varying vec2 vUv;

vec4 accumulateColor(vec2 uv, float animTime) {
  vec4 color = vec4(0.0);

  for (int i = 0; i < 4; i++) {
    vec4 pointColor = uColors[i];
    pointColor.rgb *= pointColor.a;
    vec2 point = uPoints[i].xy;
    float rad = uPoints[i].z * uPointRadiusMulti;

    point.x += sin(animTime + point.y) * uPointOffset;
    point.y += cos(animTime + point.x) * uPointOffset;

    float d = distance(uv, point);
    float pct = smoothstep(rad, 0.0, d);
    color.rgb = mix(color.rgb, pointColor.rgb, pct);
    color.a = mix(color.a, pointColor.a, pct);
  }

  return color;
}

vec3 rgb2hsv(vec3 c) {
  vec4 K = vec4(0.0, -1.0 / 3.0, 2.0 / 3.0, -1.0);
  vec4 p = mix(vec4(c.bg, K.wz), vec4(c.gb, K.xy), step(c.b, c.g));
  vec4 q = mix(vec4(p.xyw, c.r), vec4(c.r, p.yzx), step(p.x, c.r));
  float d = q.x - min(q.w, q.y);
  float e = 1.0e-10;
  return vec3(abs(q.z + (q.w - q.y) / (6.0 * d + e)), d / (q.x + e), q.x);
}

vec3 hsv2rgb(vec3 c) {
  vec4 K = vec4(1.0, 2.0 / 3.0, 1.0 / 3.0, 3.0);
  vec3 p = abs(fract(c.xxx + K.xyz) * 6.0 - K.www);
  return c.z * mix(K.xxx, clamp(p - K.xxx, 0.0, 1.0), c.y);
}

float hash(vec2 p) {
  vec3 p3 = fract(vec3(p.xyx) * 0.13);
  p3 += dot(p3, p3.yzx + 3.333);
  return fract((p3.x + p3.y) * p3.z);
}

float perlin(vec2 x) {
  vec2 i = floor(x);
  vec2 f = fract(x);
  float a = hash(i);
  float b = hash(i + vec2(1.0, 0.0));
  float c = hash(i + vec2(0.0, 1.0));
  float d = hash(i + vec2(1.0, 1.0));
  vec2 u = f * f * (3.0 - 2.0 * f);
  return mix(a, b, u.x) + (c - a) * u.y * (1.0 - u.x) + (d - b) * u.x * u.y;
}

float gradientNoise(vec2 uv) {
  return fract(52.9829189 * fract(dot(uv, vec2(0.06711056, 0.00583715))));
}

void main() {
  vec2 fragCoord = vUv * uResolution;
  vec2 normalizedUv = vec2(vUv.x, 1.0 - vUv.y);
  vec2 uv = normalizedUv;
  uv -= vec2(0.0, uTranslateY);
  uv.xy -= uBound.xy;
  uv.xy /= uBound.zw;

  vec4 color = accumulateColor(uv, uAnimTime);
  vec4 shadowMask = accumulateColor(uv + vec2(uShadowOffset), uAnimTime * 0.92);
  float noiseValue = perlin(normalizedUv * uNoiseScale + vec2(-uAnimTime, -uAnimTime));
  float shadowNoise = perlin(
    normalizedUv * uShadowNoiseScale + vec2(-uAnimTime * 0.35, uAnimTime * 0.22)
  );

  float oppositeNoise = smoothstep(0.0, 1.0, noiseValue);
  float shadowFactor = smoothstep(0.0, 1.0, shadowNoise);
  if (color.a > 0.001) {
    color.rgb /= color.a;
  } else {
    color.rgb = vec3(0.0);
  }

  vec3 hsv = rgb2hsv(color.rgb);
  hsv.y = mix(hsv.y, 0.0, oppositeNoise * uSaturateOffset);
  color.rgb = hsv2rgb(hsv);
  color.rgb += oppositeNoise * uLightOffset;

  color.a = clamp(color.a + uAlphaOffset, 0.0, 1.0);
  color.a *= uAlphaMulti;

  float shadowAlpha = clamp(
    shadowMask.a * (uShadowColorMulti + shadowFactor * uShadowColorOffset) + uAlphaOffset * 0.5,
    0.0,
    1.0
  ) * uAlphaMulti * 0.55;
  vec3 shadowRgb = clamp(
    color.rgb * (0.35 + uShadowColorMulti) + vec3(shadowFactor * uShadowColorOffset * 0.12),
    0.0,
    1.0
  );

  vec4 basePremul = vec4(color.rgb * color.a, color.a);
  vec4 shadowPremul = vec4(shadowRgb * shadowAlpha, shadowAlpha);
  vec4 combined = vec4(
    shadowPremul.rgb * (1.0 - basePremul.a) + basePremul.rgb,
    shadowPremul.a * (1.0 - basePremul.a) + basePremul.a
  );

  combined.rgb += ((1.0 / 255.0) * gradientNoise(fragCoord.xy) - (0.5 / 255.0)) * combined.a;

  gl_FragColor = combined;
}
`;
